import java.util.Properties
import java.io.FileInputStream

plugins {
    id 'com.android.application'
}

// Charger les secrets locaux uniquement si le fichier existe
def keystorePropertiesFile = rootProject.file('keystore.properties')
def keystoreProperties = new Properties()
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
}

android {
    signingConfigs {
        release {
            if (System.getenv("FDROID") != null) {
                // F-Droid fournit un keystore temporaire
                storeFile file(System.getenv("KEYSTORE"))
                storePassword System.getenv("KEYSTOREPASS")
                keyAlias System.getenv("KEYALIAS")
                keyPassword System.getenv("KEYPASS")
            } else if (keystorePropertiesFile.exists()) {
                // Compilation locale avec le keystore local
                storeFile file(keystoreProperties['storeFile'])
                storePassword keystoreProperties['storePassword']
                keyAlias keystoreProperties['keyAlias']
                keyPassword keystoreProperties['keyPassword']
            }
        }
    }

    namespace 'app.motionui.android'
    compileSdk 34
    compileSdkVersion 34

    defaultConfig {
        applicationId "app.motionui.android"
        minSdk 30
        targetSdk 34
        versionCode 100
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.6.1'
    implementation 'com.google.android.material:material:1.9.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation "androidx.core:core-splashscreen:1.0.0"
    implementation 'com.squareup.okhttp3:okhttp:4.12.0'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.5'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.5.1'
}
